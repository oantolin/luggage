;; -*- lexical-binding: t; -*-

(require 'svg)

(defun mondrian ()
  "Produce a random Mondrian-like image."
  (interactive)
  (let* ((w 400) (h 400) (mw 70) (mh 70) (sw (/ w 2)) (sh (/ h 2))
         (svg (svg-create w h)))
    (cl-labels ((split (u v)
                  (let ((p (/ (+ 33 (random 35)) 100.0)))
                    (floor (+ (* p u) (* (- 1 p) v)))))
                (color ()
                  (pcase (random 6)
                    ((or 0 1 2) "white")
                    (3 "yellow")
                    (4 "red")
                    (5 "blue")))
                (canvas (a b c d)
                  (let ((w (- c a)) (h (- d b)))
                    (cond
                      ((or (and (< w mw) (< h mh))
                           (not (or (> w sw) (> h sh) (= (random 2) 0))))
                       (svg-rectangle svg a b w h
                                      :stroke-width 5 :stroke "black"
                                      :fill-color (color)))
                      ((< w mw)
                       (let ((y (split b d)))
                         (canvas a b c y)
                         (canvas a y c d)))
                      ((< h mh)
                       (let ((x (split a c)))
                         (canvas a b x d)
                         (canvas x b c d)))
                      (t
                       (let ((x (split a c))
                             (y (split b d)))
                         (canvas a b x y)
                         (canvas x b c y)
                         (canvas a y x d)
                         (canvas x y c d)))))))
      (canvas 0 0 w h)
      (let ((buf (get-buffer-create "*Mondrian*")))
        (with-current-buffer buf (svg-insert-image svg))
        (pop-to-buffer buf)))))
